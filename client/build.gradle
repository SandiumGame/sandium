buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.gradleup.shadow:shadow-gradle-plugin:9.2.2'
    }
}

plugins {
    id "application"
    id 'java'
    id 'com.gradleup.shadow' version '9.2.2'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ':common')

    implementation 'org.ow2.asm:asm:9.9'

    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_25
    targetCompatibility = JavaVersion.VERSION_25
}

group = 'sandium'
version = sandiumVersion

ext {
    javaMainClass = "org.sandium.Main"
    javaModules = "java.base"   // Comma separated list
    itchLinuxDist = "build/dists/itch-linux"
    itchWindowsDist = "build/dists/itch-windows"
}

jar {
    manifest {
        attributes 'Main-Class': javaMainClass
    }
}

application {
    applicationDefaultJvmArgs = ['--enable-native-access=ALL-UNNAMED']
    mainClass = javaMainClass
}

shadowJar{
    archiveFileName = "Sandium.jar"
}

ext.getEnvVar = { String name, String message ->
    def glfwIncludeDir = System.getenv(name)
    if (glfwIncludeDir == null)
        throw new GradleException(message)
    return glfwIncludeDir
} as Closure<String>

tasks.register('jextract') {
    group = "Build"
    description = "Runs jextract and parses header files of native libraries "

    ext.runJextract = { includeDir, includeFile, libName, libEnum, tmpDir, srcDir, packageName ->
        delete tmpDir
        exec {
            commandLine "jextract", "--include-dir", includeDir, "--output", tmpDir, "--target-package", packageName, "--library", libName, "${includeDir}/${includeFile}"
        }

        delete srcDir
        copy {
            from tmpDir
            into "src/main/java"
            filter {
                String line -> line.replace("SymbolLookup.libraryLookup(System.mapLibraryName(\"${libName}\"), LIBRARY_ARENA)",
                        "org.sandium.core.libs.LibraryResolver.libraryLookup(org.sandium.core.libs.NativeLibrary.${libEnum}, LIBRARY_ARENA);")
                        .replace('.or(SymbolLookup.loaderLookup())', '')
                        .replace('.or(Linker.nativeLinker().defaultLookup());', '')
            }
        }
        delete tmpDir
    }

    doLast {
        runJextract(
                getEnvVar("INCLUDE_GLFW", "INCLUDE_GLFW environment variable must be set to the directory containing glfw3.h"),
                "glfw3.h",
                "glfw",
                "GLFW",
                "build/glfwjextract",
                "src/main/java/org/sandium/core/libs/glfw",
                "org.sandium.core.libs.glfw"
        )

        runJextract(
                getEnvVar("INCLUDE_VULKAN", "INCLUDE_VULKAN environment variable must be set to the directory containing vulkan.h"),
                "vulkan.h",
                "vulkan",
                "VULKAN",
                "build/vulkanjextract",
                "src/main/java/org/sandium/core/libs/vulkan",
                "org.sandium.core.libs.vulkan"
        )
    }
}

tasks.register('jdeps') {
    dependsOn shadowJar
    group = "Help"
    description = "Show required java modules"
    doLast {
        exec {
            commandLine "jdeps", "--list-deps", "build/libs/Sandium.jar"
        }
    }
}

tasks.register('packageDists') {
    dependsOn shadowJar
    group = "Build"
    description = "Create client distributable for all platforms "
    doLast {
        def linuxJdk = getEnvVar("LINUX_JDK", "LINUX_JDK environment variable must be set")
        def windowsJdk = getEnvVar("WINDOWS_JDK", "WINDOWS_JDK environment variable must be set")

        delete (String)(project.ext.itchLinuxDist)
        mkdir (String)(project.ext.itchLinuxDist)
        exec {
            commandLine "jlink", "--add-modules", project.ext.javaModules, "--output", "${project.ext.itchLinuxDist}/jre",
                    "--strip-debug", "--no-man-pages", "--no-header-files", "--compress=zip-9",
                    "--module-path", "${linuxJdk}/jmods"
        }
        copy {
            from("build/libs/Sandium.jar")
            into "${project.ext.itchLinuxDist}/lib"
        }
        copy {
            from("dist/itch/linux")
            from("dist/linux")
            from("dist/common")
            into project.ext.itchLinuxDist
        }

        delete (String)(project.ext.itchWindowsDist)
        mkdir (String)(project.ext.itchWindowsDist)
        exec {
            commandLine "jlink", "--add-modules", project.ext.javaModules, "--output", "${project.ext.itchWindowsDist}/jre",
                    "--strip-debug", "--no-man-pages", "--no-header-files", "--compress=zip-9",
                    "--module-path", "${windowsJdk}/jmods"
        }
        copy {
            from("build/libs/Sandium.jar")
            into "${project.ext.itchWindowsDist}/lib"
        }
        copy {
            from("dist/itch/windows")
            from("dist/windows")
            from("dist/common")
            into project.ext.itchWindowsDist
        }
    }
}

tasks.register('itchDeploy') {
    dependsOn packageDist
    group = "Deploy"
    description = "Deploy distributable to itch.io"
    doLast {
        exec {
            commandLine "butler", "push", project.ext.itchLinuxDist, "sandium/sandium:linux", "--userversion", sandiumVersion
        }
        exec {
            commandLine "butler", "push", project.ext.itchWindowsDist, "sandium/sandium:windows", "--userversion", sandiumVersion
        }
    }
}