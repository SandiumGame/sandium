plugins {
    id "application"
    id 'java'
    id 'com.gradleup.shadow' version '8.3.1'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ':common')

    implementation group: 'org.ow2.asm', name: 'asm', version: '9.7.1'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_22
    targetCompatibility = JavaVersion.VERSION_22
}

group = 'sandium'
version = sandiumVersion

ext {
    javaMainClass = "org.sandium.Main"
    javaModules = "java.base"   // Comma separated list
    itchLinuxDist = "build/dists/itch-linux"
    itchWindowsDist = "build/dists/itch-windows"
}

jar {
    manifest {
        attributes 'Main-Class': javaMainClass
    }
}

application {
    applicationDefaultJvmArgs = ['--enable-native-access=ALL-UNNAMED']
    mainClass = javaMainClass
}

shadowJar{
    archiveFileName = "Sandium.jar"
}

ext.getEnvVar = { String name, String message ->
    def glfwIncludeDir = System.getenv(name)
    if (glfwIncludeDir == null)
        throw new GradleException(message)
    return glfwIncludeDir
} as Closure<String>

tasks.register('jextract') {
    group = "Build"
    description = "Runs jextract and parses header files of native libraries "
    doLast {
        def glfwIncludeDir = getEnvVar("INCLUDE_GLFW", "INCLUDE_GLFW environment variable must be set to the directory containing glfw3.h")
        def tmpDir = "build/glfwjextract"

        delete tmpDir
        exec {
            commandLine "jextract", "--include-dir", glfwIncludeDir, "--output", tmpDir, "--target-package", "org.sandium.core.libs.glfw", "--library", "glfw", "-D", "GLFW_INCLUDE_VULKAN=1", "${glfwIncludeDir}/glfw3.h"
        }

        delete "src/main/java/org/sandium/core/libs/glfw"
        copy {
            from tmpDir
            into "src/main/java"
            filter {
                String line -> line.replace('SymbolLookup.libraryLookup(System.mapLibraryName("glfw"), LIBRARY_ARENA)',
                                'org.sandium.core.libs.LibraryResolver.libraryLookup(org.sandium.core.libs.NativeLibrary.GLFW, LIBRARY_ARENA);')
                                .replace('.or(SymbolLookup.loaderLookup())', '')
                                .replace('.or(Linker.nativeLinker().defaultLookup());', '')
            }
        }
        delete tmpDir
    }
}

tasks.register('jdeps') {
    dependsOn shadowJar
    group = "Help"
    description = "Show required java modules"
    doLast {
        exec {
            commandLine "jdeps", "--list-deps", "build/libs/Sandium.jar"
        }
    }
}

tasks.register('packageDists') {
    dependsOn shadowJar
    group = "Build"
    description = "Create client distributable for all platforms "
    doLast {
        def linuxJdk = getEnvVar("LINUX_JDK", "LINUX_JDK environment variable must be set")
        def windowsJdk = getEnvVar("WINDOWS_JDK", "WINDOWS_JDK environment variable must be set")

        delete (String)(project.ext.itchLinuxDist)
        mkdir (String)(project.ext.itchLinuxDist)
        exec {
            commandLine "jlink", "--add-modules", project.ext.javaModules, "--output", "${project.ext.itchLinuxDist}/jre",
                    "--strip-debug", "--no-man-pages", "--no-header-files", "--compress=zip-9",
                    "--module-path", "${linuxJdk}/jmods"
        }
        copy {
            from("build/libs/Sandium.jar")
            into "${project.ext.itchLinuxDist}/lib"
        }
        copy {
            from("dist/itch/linux")
            from("dist/linux")
            from("dist/common")
            into project.ext.itchLinuxDist
        }

        delete (String)(project.ext.itchWindowsDist)
        mkdir (String)(project.ext.itchWindowsDist)
        exec {
            commandLine "jlink", "--add-modules", project.ext.javaModules, "--output", "${project.ext.itchWindowsDist}/jre",
                    "--strip-debug", "--no-man-pages", "--no-header-files", "--compress=zip-9",
                    "--module-path", "${windowsJdk}/jmods"
        }
        copy {
            from("build/libs/Sandium.jar")
            into "${project.ext.itchWindowsDist}/lib"
        }
        copy {
            from("dist/itch/windows")
            from("dist/windows")
            from("dist/common")
            into project.ext.itchWindowsDist
        }
    }
}

tasks.register('itchDeploy') {
    dependsOn packageDist
    group = "Deploy"
    description = "Deploy distributable to itch.io"
    doLast {
        exec {
            commandLine "butler", "push", project.ext.itchLinuxDist, "sandium/sandium:linux", "--userversion", sandiumVersion
        }
        exec {
            commandLine "butler", "push", project.ext.itchWindowsDist, "sandium/sandium:windows", "--userversion", sandiumVersion
        }
    }
}